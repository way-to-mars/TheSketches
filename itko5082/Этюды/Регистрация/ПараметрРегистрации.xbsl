@Обработчик
метод ПослеПодключения()

    исп КонтекстДоступа.Привилегированный()

    знч ПользовательСсылка = Пользователи.ТекущийПользователь
    знч ПользовательОбъект = ПользовательСсылка.ЗагрузитьОбъект()
    знч ПользовательИдСписка = Пользователи.ТекущийИдПользователя.ИдСписка

    Логи.Информация("ПослеПодключения", "%ПользовательСсылка - %ПользовательИдСписка\н%ПользовательОбъект", "%Имя %Фамилия")

    знч НоваяУчетнаяЗапись = новый УчетнаяЗапись.Объект(
        Ид = Ууид.Версия7(),
        Пользователь = ПользовательСсылка,
        ПоставщикАутентификации = ПоставщикиАутентификации.Локальный,
        Идентификатор = Имейл,
        Имя = Имя,
        Фамилия = Фамилия,
        Имейл = Имейл,
        Фото = ""
    )
    попытка
        НоваяУчетнаяЗапись.Записать()
    поймать Искл: Исключение
        Логи.Ошибка("ПослеПодключения", Искл.Описание, НоваяУчетнаяЗапись.Представление())
        возврат
    ;

    знч НовыйПрофиль = новый ПрофилиПользователей.Объект(
        Имя = Имя,
        Фамилия = Фамилия,
        Имейл = Имейл,
        Фото = "",
        ДатаРождения = ДатаРождения,
        Язык = Язык,
        ПризнакЗавершенаРегистрация = Ложь,
        ПризнакЗаблокирован = Ложь
        )

    попытка
        НовыйПрофиль.Записать()
    поймать Искл: Исключение
        Логи.Ошибка("ПослеПодключения", Искл.Описание, НовыйПрофиль.Представление())
        возврат
    ;

    Логи.Информация("ПослеПодключения", НовыйПрофиль.Представление(), "Новый профиль")

;

@Обработчик
метод ПриПроверкеПараметра(Логин: Строка?, Контакты: ЧитаемоеСоответствие<ВидКонтактаПользователя, Строка>): Булево

    если Имя.Пусто()
        Логи.Ошибка("ПриПроверкеПараметра", "Имя пустое", "Логин: %{Логин}")
        возврат Ложь
    ;

    если Фамилия.Пусто()
        Логи.Ошибка("ПриПроверкеПараметра", "Фамилия пустая", "Логин: %{Логин}")
        возврат Ложь
    ;

    если Контакты.ПолучитьИлиУмолчание(ВидКонтактаПользователя.Почта, "").Пусто()
        Логи.Ошибка("ПриПроверкеПараметра", "Не указан имейл", "Логин: %{Логин}")
        возврат Ложь
    ;

    если Логин?.Пусто() ?? Истина
        Логи.Ошибка("ПриПроверкеПараметра", "Отсутствует логин", "Контакты: ${Контакты}")
        возврат Ложь
    ;

    Логи.Информация("ПриПроверкеПараметра", Логин, Контакты.Представление())

    возврат Истина
;
