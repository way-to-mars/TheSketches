импорт ПользовательскоеСоглашение


метод СогласиеПриНажатии(Источник: Надпись, Событие: СобытиеПриНажатии)
    знч Ссылка = НавигационнаяСсылка.Абсолютная(Тип<ПриложениеПользовательскоеСоглашение>)
    знч КодЯзыка = Язык.Текущий().Код

    ПерейтиПоСсылке("%{Ссылка}?lang=%{КодЯзыка}", ОткрыватьВНовомОкне = Истина)
;

метод МаксимумШириныГруппыРегистрации(): Авто|Число
    возврат КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон
        ? Авто
        : 500
;

метод СоздатьПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)

    если Имейл.Пусто()
        Компоненты.ПолеИмейл.Активировать()
        ТостОшибка(ЛокализованныеСтрокиРегистрации.ОшибкаИмейлПустой())
        возврат
    ;

    если не ПроверкаEmail(Имейл)
        Компоненты.ПолеИмейл.Активировать()
        ТостОшибка(ЛокализованныеСтрокиРегистрации.ОшибкаИмейлФормат())
        возврат
    ;

    если Пароль.Пусто()
        Компоненты.ПолеПароль.Активировать()
        ТостОшибка(ЛокализованныеСтрокиРегистрации.ОшибкаПарольПустой())
        возврат
    ;

    если Пароль.Длина() < 8
        Компоненты.ПолеПароль.Активировать()
        ТостОшибка(ЛокализованныеСтрокиРегистрации.ОшибкаПарольКороткий())
        возврат
    ;

    если Имя.Пусто()
        Компоненты.ПолеИмя.Активировать()
        ТостОшибка(ЛокализованныеСтрокиРегистрации.ОшибкаИмяПустое())
        возврат
    ;

    если Фамилия.Пусто()
        Компоненты.ПолеФамилия.Активировать()
        ТостОшибка(ЛокализованныеСтрокиРегистрации.ОшибкаФамилияПустая())
        возврат
    ;

    если ДатаРождения == Неопределено
        Компоненты.ПолеДатаРождения.Активировать()
        ТостОшибка(ЛокализованныеСтрокиРегистрации.ОшибкаДатаРожденияПустая())
        возврат
    ;

    если ДатаРождения>= Дата.Сейчас()
        Компоненты.ПолеДатаРождения.Активировать()
        ТостОшибка(ЛокализованныеСтрокиРегистрации.ОшибкаДатаРожденияБудущее())
        возврат
    ;

    если не Согласие
        ТостОшибка(ЛокализованныеСтрокиРегистрации.ОшибкаНетСогласия())
        возврат
    ;

    Зарегистрировать()
;

@Локально
метод Зарегистрировать()
    Регистрация = ОперацииСамообслуживания.РегистрацияПользователя()

    знч СтатусРегистрации = Регистрация.Начать(
        Логин = Имейл,
        Пароль = Пароль,
        СпособПодтверждения = СпособПодтвержденияОперации.Почта,
        КонтактПодтверждения = "%{Имя} %{Фамилия}",
        Параметр = новый ПараметрРегистрации(Имя = Имя, Фамилия = Фамилия, ДатаРождения = ДатаРождения)
    )

    если не СтатусРегистрации.ПредыдущийШагУспешен
        ТостОшибка(ЛокализованныеСтрокиРегистрации.ОшибкаРегистрации())
        Логи.Ошибка("Зарегистрировать", ДанныеСтатуса(СтатусРегистрации), ДанныеРегистрации())
        возврат
    ;

    КодПодтверждения = ""
    ОтображатьПодтверждение = Истина
;

метод ПодтвердитьПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    если КодПодтверждения.Пусто()
        ТостОшибка(ЛокализованныеСтрокиРегистрации.ПодтверждениеОшибкаПустойКод())
        возврат
    ;

    если Регистрация == Неопределено
        Логи.Ошибка("ПодтвердитьПриНажатии", "Регистрация = Неопределено", ДанныеРегистрации())
        НазадКРедактированию()
        возврат
    ;

    знч СтатусПодтверждения = Регистрация.ОтправитьПодтверждение(КодПодтверждения)
    если не СтатусПодтверждения.ПредыдущийШагУспешен
        если СтатусПодтверждения.ОставшиесяПопытки > 0
            Логи.Информация("ПодтвердитьПриНажатии", ДанныеСтатуса(СтатусПодтверждения), ДанныеРегистрации())
            ТостОшибка(ЛокализованныеСтрокиРегистрации.ПодтверждениеОшибкаНеверныйКод() + СтатусПодтверждения.ОставшиесяПопытки)
        иначе
            Логи.Ошибка("ПодтвердитьПриНажатии", ДанныеСтатуса(СтатусПодтверждения), ДанныеРегистрации())
            ТостОшибка(ЛокализованныеСтрокиРегистрации.ПодтверждениеОшибкаПопыткиЗакончились())
        ;
        возврат
    ;

    // знч Ссылка = НавигационнаяСсылка.Абсолютная(Тип<>)
    Регистрация.Завершить()
    Сообщить("Успешная регистрация")
;

метод ДанныеРегистрации(): Строка
    знч МассивСтрок = [Имейл, "Длина пароля: %{Пароль.Длина()}", Имя, Фамилия, ДатаРождения.ВСтроку()]
    возврат Строки.Соединить(МассивСтрок, "; ")
;

метод ДанныеСтатуса(Статус: СтатусПодтверждаемойОперацииСамообслуживания): Строка
    возврат "Шаг: ${Статус.Шаг}, Сообщение: %{Статус.Сообщение}"
;

@Локально
метод НазадКРедактированию()
    ОтображатьПодтверждение = Ложь
    КодПодтверждения = ""
    Регистрация = Неопределено
;

метод ПроверкаEmail(Значение: Строка): Булево
    знч ОбразецEmail = '^[^\п@]+@[^\п@]+\.[^\п@]+$'
    знч Совпадения = ОбразецEmail.НайтиСовпадения(Значение)

    возврат Совпадения.Размер() == 1
;

метод ТостОшибка(Текст: Строка)
    знч Уведомление = новый Уведомление("", Текст)
    Уведомление.Вид = ВидУведомления.Всплывающее
    Уведомление.КлючУникальности = этот.Представление()
    Уведомление.ОценкаИнформации = ОценкаИнформации.Предупреждающая
    Уведомление.Показать()
;

метод ВводКодаПриИзменении(Источник: ПолеВвода<Строка>, Событие: СобытиеПриИзменении<Строка>)
    Источник.Значение = ОставитьТолькоЦифры(Событие.НовоеЗначение)
;

метод ВводКодаПриИзмененииТекстаРедактирования(Источник: ПолеВвода<Строка>, Событие: СобытиеСДанными<Строка>)
    Источник.Значение = ОставитьТолькоЦифры(Событие.Данные)
;

статический метод ОставитьТолькоЦифры(Источник: Строка): Строка
    если Источник.ТолькоЦифры()
        возврат Источник
    ;

    знч МассивСимволов = <Строка>[]
    для Индекс = 0 по Источник.Длина() - 1
        знч Символ = Источник.Символ(Индекс)
        если Символ.ТолькоЦифры()
            МассивСимволов.Добавить(Символ)
        ;
    ;
    возврат Строки.Соединить(МассивСимволов)
;